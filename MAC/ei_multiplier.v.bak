`timescale 1ns / 1ps

module ei_multiplier (
    input         sys_clk,
    input         rst,      
    input         en,       
    input  [7:0]  a_in,
    input  [7:0]  b_in,
    output [15:0] c_out
);
    // --- STAGE 0: Latch Input ---
    wire [7:0] a_s0, b_s0;
    regN #(.WIDTH(8)) reg_a_s0 (.clk(sys_clk), .rst(rst), .en(en), .d(a_in), .q(a_s0));
    regN #(.WIDTH(8)) reg_b_s0 (.clk(sys_clk), .rst(rst), .en(en), .d(b_in), .q(b_s0));

    // --- STAGE 1: Tích riêng & Cộng tầng 1 ---
    wire [15:0] r0 = b_s0[0] ? {8'b0, a_s0}       : 16'b0;
    wire [15:0] r1 = b_s0[1] ? {7'b0, a_s0, 1'b0} : 16'b0;
    wire [15:0] r2 = b_s0[2] ? {6'b0, a_s0, 2'b0} : 16'b0;
    wire [15:0] r3 = b_s0[3] ? {5'b0, a_s0, 3'b0} : 16'b0;
    wire [15:0] r4 = b_s0[4] ? {4'b0, a_s0, 4'b0} : 16'b0;
    wire [15:0] r5 = b_s0[5] ? {3'b0, a_s0, 5'b0} : 16'b0;
    wire [15:0] r6 = b_s0[6] ? {2'b0, a_s0, 6'b0} : 16'b0;
    wire [15:0] r7 = b_s0[7] ? {1'b0, a_s0, 7'b0} : 16'b0;

    wire [15:0] sum01, sum23, sum45, sum67;
    ei_adder16 add0 (.a(r0), .b(r1), .sum(sum01), .cout());
    ei_adder16 add1 (.a(r2), .b(r3), .sum(sum23), .cout());
    ei_adder16 add2 (.a(r4), .b(r5), .sum(sum45), .cout());
    ei_adder16 add3 (.a(r6), .b(r7), .sum(sum67), .cout());

    wire [15:0] r_sum01, r_sum23, r_sum45, r_sum67;
    regN #(.WIDTH(16)) reg_s1_0 (.clk(sys_clk), .rst(rst), .en(en), .d(sum01), .q(r_sum01));
    regN #(.WIDTH(16)) reg_s1_1 (.clk(sys_clk), .rst(rst), .en(en), .d(sum23), .q(r_sum23));
    regN #(.WIDTH(16)) reg_s1_2 (.clk(sys_clk), .rst(rst), .en(en), .d(sum45), .q(r_sum45));
    regN #(.WIDTH(16)) reg_s1_3 (.clk(sys_clk), .rst(rst), .en(en), .d(sum67), .q(r_sum67));

    // --- STAGE 2: Cộng tầng 2 ---
    wire [15:0] sum0123, sum4567;
    ei_adder16 add4 (.a(r_sum01), .b(r_sum23), .sum(sum0123), .cout());
    ei_adder16 add5 (.a(r_sum45), .b(r_sum67), .sum(sum4567), .cout());

    wire [15:0] r_sum0123, r_sum4567;
    regN #(.WIDTH(16)) reg_s2_0 (.clk(sys_clk), .rst(rst), .en(en), .d(sum0123), .q(r_sum0123));
    regN #(.WIDTH(16)) reg_s2_1 (.clk(sys_clk), .rst(rst), .en(en), .d(sum4567), .q(r_sum4567));

    // --- STAGE 3: Output ---
    wire [15:0] final_sum;
    ei_adder16 add6 (.a(r_sum0123), .b(r_sum4567), .sum(final_sum), .cout());
    regN #(.WIDTH(16)) reg_out (.clk(sys_clk), .rst(rst), .en(en), .d(final_sum), .q(c_out));
    // Latency = 4 chu kỳ
endmodule